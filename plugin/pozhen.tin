#var pozhen.pfm {cuff.zhen};

#alias ask.job {
    #class ask.job open;
    #action {你对陆乘风道：“听闻近来有人冒名桃花岛弟子在%S布下迷阵行恶，我愿代劳除去此獠。”} {
        #var jobplace %%1;
        zhanbu.place;
        #class ask.job kill;
    };
    #class ask.job close;
    ask lu about 破阵;
};

#alias ask.done {ask lu about 破阵奖励; gf bosss; dazuo max};
#alias ask.fail {ask lu about 破阵失败; gf bosss; dazuo max};

#alias zhanbu.place {
    quest-log 破阵地点: $jobplace;
    #echo <cfa><EAA>=================================================;
    #echo <cfa><EAA>　　破阵地点：<AAA>　$jobplace;
    #echo <cfa><EAA>=================================================;
};

#alias zhanbu.maze {
    #class zhanbu.maze open;
    #var pos {{nw}{1}{n}{1}{ne}{1}{w}{1}{e}{1}{sw}{1}{s}{1}{se}{1}};
    #var mazeline {n};

    #var direc {nw;n;ne;w;e;sw;s;se};
    #foreach {$direc} {direction} {
        #local cmd {#alias $direction {#var pace $direction;#send $direction}};
        $cmd;
    };

    #function printdirec {
        #var printtext {　　经推算，正确方向可能为：};
        #foreach {$direc} {direction} {
            #if {$pos[$direction]} {
                #var printtext {$printtext $direction};
            };
        };
        #echo <cfa><EAA>=================================================;
        #echo <cfa>$printtext;
        #echo <cfa><EAA>=================================================;
        #return #cr;
    };

    #action {~{\e\[5m|}\e[42%S{\e\[5m|}\e[42%S{\e\[5m|}\e[42} {
        #if {"%%1" == ""} {#var pos[${mazeline}w] 0};
        #if {"%%3" == ""} {#var pos[$mazeline]    0};
        #if {"%%5" == ""} {#var pos[${mazeline}e] 0};

        #if { "$mazeline" == "n" } {
            #var mazeline {s};
        };
        #else {
            #var mazeline {n};
            @printdirec{};
        };
    } {1};

    #action {~{\e\[5m|}\e[42%S{\e\[5m|}\e[42} {
        #if {"%%1" == ""} {#var pos[w] 0};
        #if {"%%3" == ""} {#var pos[e] 0};
    } {2};

    #action {^你尝试破阵，可惜选择了错误的方向，损失了一些气血和内力。$} {
        #var pos[$pace] 0;
    };

    #action {^这里的迷阵在你眼中如同儿戏一般。$} {
        #var pos {{nw}{1}{n}{1}{ne}{1}{w}{1}{e}{1}{sw}{1}{s}{1}{se}{1}};
        #var mazeline {n};
    };

    #action {^破阵失败！这里的真相恐怕永远被掩盖。$} {
        #class zhanbu.maze kill;
        #echo <cfa><EAA>=================================================;
        #echo <cfa>　　破阵失败！;
        #echo <cfa><EAA>=================================================;
        unset ForcedAct
    };

    #action {^你彻底破解了这里的迷阵！$} {
        #echo <cfa><EAA>=================================================;
        #echo <cfa>　　破阵成功！;
        #echo <cfa><EAA>=================================================;
    };

    #action {^看起来%S想杀死你！$} {
        #class zhanbu.maze kill;
        #class zhanbu.maze.kill open;
        #action {大 恶 %%1(%*)} {
            #unaction {大 恶 %%1(%*)};
            #var pozhen-npcid {};
            #format pozhen-npcid {%l} {%%%1};
            #delay 0.5 {
                kill ${pozhen-npcid};
                fight.begin;
                perform ${pozhen.pfm}
            };
        };
		#action {^%%1道：想不到我最终还是失败了！哎！$} {
			#class zhanbu.maze.kill kill;
			#echo <cfa><EAA>=================================================;
			#echo <cfa>　　破阵任务成功完成！;
			#echo <cfa><EAA>=================================================;
			s; #delay 1 {unset ForcedAct; wear shield}
		};  
		#action {^你在攻击中不断积蓄攻势。(气势：%d%)$} {
			#if {%%%1 >= 8} {perform ${pozhen.pfm}};
		};
        #class zhanbu.maze.kill close;
        #delay 1 {perform ${pozhen.pfm}}
    };

    #class zhanbu.maze close;
    jiali max;
    yun recover;
    yun regenerate;
    yun qi;
    yun powerup;
    #NOP yun taiji;
    #NOP yun shield;
    #NOP perform dodge.zong;
    #nop unset skip_combat;
    #nop remove shield;
    #nop set ForcedAct;
    zhanbu -maze;
};
