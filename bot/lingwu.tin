#class lianxi open;
#show {<efa>必须在门派指定领悟室方可使用。};
#list {basic.skills} {create} {blade} {dodge} {force} {strike} {parry} {staff} {hand}
#var {lingwu.delay} {2};

#alias {_lian} {
    lingwu ${lingwu.skill} 10;
};

#alias {startbot} {
    #if {"%1" == "blade" | "%1" == "dodge" | "%1" == "force" |"%1" == "strike" | "%1" == "parry" | "%1" == "staff" | "%1" == "hand"} {
        #var {lingwu.skill} {%1}; 

        #if {"%2" != ""} {
            #var {lingwu.delay} {%2};
        };

        #action {^你的基本功夫比你的高级功夫还高！$} { 
            #class lianxi kill;
            loadbot lian; 
            #show {<efa>正在切换到高级功夫练习...};
            #delay 1 {startbot ${lingwu.skill} %2};
        };

        #show {<efa>skill: ${lingwu.skill}, delay: ${lingwu.delay}};
        #show {<efa>开始领悟基础武功...};
    };
    #else {
        #echo {%c使用方法：startbot [staff|blade|dodge|strike|parry|hand] <每次间隔秒数>} {light yellow};
    };
};

#alias {stopbot} {
    #show {<efa>结束领悟基础武功...};
    #class lianxi kill;
};

#action {^开始领悟基础武功...$} {_lian};
#action {^你领悟了%1次%2。$} { #delay ${lingwu.delay} {_lian} };
#action {^你现在过于疲倦,无法静下心来领悟功夫！$} { yun regenerate };
#action {^你正在运行真气加速气血恢复，无法再分出内力来。$} { #delay 30 {yun regenerate;} };
#action {^你略一凝神，精神看起来好多了。$} { #delay ${lingwu.delay}  {_lian} };
#action {^你的%1太低了。$} { #delay 5 {_lian} };

#action {^你的内力不够。$} { _sleep };
#action {^( 你的内力已经枯竭，不能施用内功。)$} { _sleep };
#action {^你一觉醒来，精神抖擞地活动了几下手脚。$} { #delay 3 {_lian}};
#action {你精血不足，翻来覆去无法入眠!} { #echo {%c糟糕！失眠了，你决定稍等10s再试一次...} {light red}; #delay 10 {sleep} };
#action {你刚在三分钟内睡过一觉, 多睡对身体有害无益!} { #delay 5 {sleep} };

#class lianxi close;
