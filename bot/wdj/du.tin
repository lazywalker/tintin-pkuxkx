#class bot open;
#var {du.delay} {2};

#alias {_lian} {
    #if {"${du.book}" == "shediao"} {
        du ${du.book} 50;
    }; #elseif {"${du.book}" == "tianlong"} {
        read ${du.book} 10;
    }; #else {
        du ${du.book} for 1;
    };
};

#alias {_recover} {
   w;have;nd;n;drink;
   s;se;
   sleep
};

#alias {startbot} {
    #if {"%1" == ""} {
        #echo {%c使用方法：startbot [book id(英文)] <每次间隔秒数>} {light yellow};
    };
    #else {
        #var {du.book} {%1};
        #if {"%2" != ""} {
            #var {du.delay} {%2};
        };
        set brief 3;
        #show {<fca>book:${du.book}|delay:${du.delay}};
        #show {<efa>开始读书...};
    };
};

#alias {stopbot} {
    #show {<efa>结束读书...};
    set brief 0;
    #class bot kill;
};

#action {^开始读书...$} {_lian};
#action {^你研读了一次有关%1的技巧，似乎有点心得。$} { #delay ${du.delay} {_lian} };
#action {^你思索着小说中关于%2描述，对%3有一些新的理解。$} { #delay ${du.delay} {_lian} };
#action {^你已经很累了，歇歇再读吧，身体要紧。$} { _recover };

#action {^你现在过于疲倦，无法专心下来研读新知。$} { _recover };

#action {^你一觉醒来，精神抖擞地活动了几下手脚。$} { #delay 2 {_lian}};

#action {你刚在三分钟内睡过一觉, 多睡对身体有害无益!} { #delay 5 {sleep} };

#class bot close;
