#class lianxi open;
#var {lian.delay} {2};

#alias {_lian} {
    lian ${lian.skill} 50;
};

#alias {startbot} {
    #if {"%1" == "blade" | "%1" == "dodge" | "%1" == "strike" | "%1" == "parry" | "%1" == "staff" | "%1" == "hand"} {
        #var {lian.skill} {%1}; 

        #if {"${lian.skill}" == "hand" | "${lian.skill}" == "strike"} {
            #action {^你的内力不够练%*。$} { _sleep };
            #action {^你将运转于全身经脉间的内息收回丹田，深深吸了口气，站了起来。$} { #delay 2 {yun qi; _lian} };
            #action {^你一觉醒来，精神抖擞地活动了几下手脚。$} { #delay 2 {dz} };
        };#else { 
            #if {"${lian.skill}" == "dodge"} {
                #action {^你现在气血虚浮，练习不了%*。$} { #delay 5 {_lian} };
                #action {^你太累了，没有办法练习%*。$} { #delay 5 {_lian} };
            };
            #action {^你的体力太低了。$} { #delay 5 {_lian} };
            #action {^你的体力不够练%*。$} { #delay 5 {_lian} };
            #action {^你一觉醒来，精神抖擞地活动了几下手脚。$} { #delay 2 {yun qi; _lian}};
        };

        #action {^你需要提高基本功，不然练得再多也没有用。$} { 
            #class lianxi kill;
            loadbot lingwu; 
            #show {<efa>正在切换到基础功夫领悟...};
            #delay 1 {startbot ${lian.skill} ${lian.delay}};
        };

        #if {"%2" != ""} {
            #var {lian.delay} {%2};
        };
        #show {<efa>skill: ${lian.skill}, delay: ${lian.delay}};
        #show {<efa>开始练习高级武功...} ;
    };
    #else {
        #echo {%c使用方法：startbot [staff|blade|dodge|strike|parry|hand]  <每次间隔秒数>} {light yellow};
    };
};

#alias {stopbot} {
    #show {<efa>结束练习高级武功...};
    #class lianxi kill;
};

#action {^开始练习高级武功...$} { yun qi; _lian};
#action {^你%1练习%2，获得了%3进步！$} { #delay ${lian.delay} {_lian} };

#action {^你减缓真气运行，让气血运行恢复正常。$} {
    #if {"${lian.skill}" == "dodge" | "${lian.skill}" == "staff" | "${lian.skill}" == "blade"} {
        yun recover;
    }; 
    
    yun qi; 
};

#action {^你的%1太低了。$} { #delay 5 {_lian} };

#action {^( 你的内力已经枯竭，不能施用内功。)$} { _sleep };
#action {你精血不足，翻来覆去无法入眠!} { #echo {%c糟糕！失眠了，你决定稍等10s再试一次...} {light red}; #delay 10 {sleep} };
#action {你刚在三分钟内睡过一觉, 多睡对身体有害无益!} { #delay 5 {sleep} };

#class lianxi close;
