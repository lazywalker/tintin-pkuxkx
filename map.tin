#class $mod_name open;

#alias {${mod_name}_load} {
    #showme Module $mod_name loaded;
    #unalias ${mod_name}_load;
};

#alias {${mod_name}_unload}{
    map stop;
    #class $mod_name kill;
    #showme Module $mod_name unloaded;
};

#nop 红花老祖本姓朱 为救苍生下凡来;

#nop ============ Module variables start ======================================;

#var map[status][mapping] 0;

#var map[room][name] {};
#var map[room][exit] {};
#var map[room][desc] {};
#var map[room][area] {};
#var map[room][unit] {};
#var map[room][item] {};
#var map[room][indoor] 0;
#var map[room][season] {};
#var map[room][weather] {};

#var map[gt][status] noaction;
#var map[gt][path] {};

#nop ============ Module funtion start ========================================;

#function {get_room} {
    INVOKE map.search %1 %2;
};

#function {format_exits} {
    #var format_exits_inner[estr] %1;
    
    #replace {format_exits_inner[estr]} {。} {};
	#replace {format_exits_inner[estr]} { } {};
	#replace {format_exits_inner[estr]} {和} { };
	#replace {format_exits_inner[estr]} {、} { };
	
	#replace {format_exits_inner[estr]} {northeast} {ne};
	#replace {format_exits_inner[estr]} {northwest}{nw};
	#replace {format_exits_inner[estr]} {southeast}{se};
	#replace {format_exits_inner[estr]} {southwest}{sw};
    #replace {format_exits_inner[estr]} {northdown}{nd};
    #replace {format_exits_inner[estr]} {southdown}{sd};
    #replace {format_exits_inner[estr]} {eastdown}{ed};
    #replace {format_exits_inner[estr]} {westdown}{wd};
    #replace {format_exits_inner[estr]} {southup}{su};
    #replace {format_exits_inner[estr]} {northup}{nu};
	#replace {format_exits_inner[estr]} {eastup}{eu};
    #replace {format_exits_inner[estr]} {westup}{wu};
	#replace {format_exits_inner[estr]} {south}{s};
	#replace {format_exits_inner[estr]} {north}{n};
    #replace {format_exits_inner[estr]} {down}{d};
	#replace {format_exits_inner[estr]} {east}{e};
	#replace {format_exits_inner[estr]} {west}{w};
    #replace {format_exits_inner[estr]} {up}{u};
    
    #list result create $format_exits_inner[estr];
    #unvar format_exits_inner;
};

#nop ============ Module private function start ===============================;

#alias {_hnd_roomdesc} {
    #nop #regexp {%1} {^%S%s-%s$};
    #regexp {%1} {^%* -  $} {
        #nop #send {get all};
        #var hnd_addesc_inner[desc_start] 1;
        #var hnd_addesc_inner[desc] {};
        #var map[room][unit] {};
        #var map[room][indoor] 1;
        #var map[room][season] {};
		#var map[room][weather] {};
		#var map[room][item] {};
        #var map[room][name] &1;
    } {
    #nop dbg $hnd_addesc_inner[desc_start];
        #if {&hnd_addesc_inner[desc_start] != 0} {
            #regexp {%1} {^%s这里%*{方向|出口}{是|有}%*} {
                #var hnd_addesc_inner[desc_start] 0;
                #var map[room][exit] @format_exits{&5};
            };
            #regexp {%1} {^%s「%*」: %*。$} {
                #var hnd_addesc_inner[desc_start] 0;
                #var map[room][indoor] 0;
				#var map[room][season] &2;
				#var map[room][weather] &3;
            };
            #regexp {%1}{^%s你可以看看(look):%*$}
			{
				#nop #var hnd_addesc_inner[desc_start] 0;
				#var map[room][item] {&2};

				#replace {map[room][item]}{,}{;};
			};
            #if {$hnd_addesc_inner[desc_start] == 1} {
                #var hnd_addesc_inner[desc] @trim{$hnd_addesc_inner[desc]%1};
                #unvar result;
            };
            #regexp {%1} {^%s%S「%S」%S(%w %w)%*}
			{
				#var map[room][unit][&5 &6] &4;
			}{
				#regexp {%1} {^%s%S(%w %w)%*}
				{
					#var map[room][unit][&3 &4] &2;
				}{
					#regexp {%1} {^%s%S%s%S(%w %w)%*}
					{
						#var map[room][unit][&5 &6] &4;
					}{
						#regexp {%1}{^%s%S%s%S%s%S(%w %w)%*}
						{
							#var map[room][unit][&7 &8] &6;
						};
					};
				};
			};
			
			#nop exclude items in units;
			#unvar map[room][unit][Huo tong];
			#unvar map[room][unit][Yun tie];
			#unvar map[room][unit][Shi zi];
			#unvar map[room][unit][Xiuhua bengjia];
			#unvar map[room][unit][Jin hua];
			
            #regexp {%1} {^>%s} {
                #var map[room][desc] $hnd_addesc_inner[desc];
                #unvar hnd_addesc_inner;
                #showme <139>->_hnd_roomdesc : Get room desc complete<079>;
                #showme ->;
            }; 
        };
    };
};

#nop ============ Module alias start ==========================================;

#alias {map} {
    #switch {"%1"} {
        #case {"load"} {
            #map read data/pku.map;
            #map return;
            map pad;
            dbg map data loaded from data/pku.map;
        };
        #case {"save"} {
            #map write data/pku.map;
            
            dbg map data saved to data/pku.map;
        };
        #case {"reload"} {
            #map destroy;
            map load;
            dbg map data reloaded from data/pku.map;
        };
        #case {"stop"} {
            event_handler_del {_hnd_roomdesc} {RECEIVED LINE};
            #if {$map[status][mapping] == 1} {
                #var map[status][mapping] 0;
                ades stop;
                
                #class map_start_inner kill;
            };
            #class ad_inner kill;
            #nop mod bag load;
            dbg mapping stopped;
        };
        #case {"pad"} {
            #alias {mappad} {
                #nop #map goto {$map[room][name]}{}{%*$map[room][desc]%*}{$map[room][area]};
                #map goto {$map[room][name]}{}{}{$map[room][area]};
                show_room_data;
                #unalias mappad;
                dbg map padding complete;
            };
            #if {$map[status][mapping] == 0} {
                get_area {
                    on_get_room {
                        mappad;
                    };
                };
            };
            #elseif {$map[status][mapping] == 1} {
                get_area {
                    mappad;
                };
            };
            
        };
        #case {"start"} {
            
            #nop mod bag unload;
            get_area;
            dbg start mapping;
            
            #var map[status][mapping] 1;
            
            event_handler_add {_hnd_roomdesc} {RECEIVED LINE};
            
            #class map_start_inner open;
            
            #var ades 0;
            
            #act {_hnd_roomdesc : Get room desc complete} {
                #nop get_area;
                #if {$adnew == 1} {
                    #undelay ad_inner_watchdog;
                    
                    #map dig {$chukou} {new};
                    #map move $chukou;
                    #map name $map[room][name];
                    #map set roomarea $map[room][area];
                    #map set roomdesc $map[room][desc];
                    #map set roomdata $map[room][unit];
                    
                    #var timeout 0;
                    #class ad_inner kill;
                };
                #if {$ades == 1} {
                    ades;
                    
                };
                
                show_room_data;
            };
            #alias {ada} {
                #if {"%%1" != ""} {
                    #var map[room][area] %%1;
                    #showme <139>->room area been changed to : %%1<079>;
                } {
                    #showme <139>->current area : $map[room][area]<079>;
                };
            };
            #alias {ades} {
                #if {"%%1" == "start"} {
                    #var ades 1;
                    dbg roomdesc auto-store starting;
                };
                #elseif {"%%1" == "stop"} {
                    #var ades 0;
                    dbg roomdesc auto-store stopped;
                };
                #else {
                    #map name $map[room][name];
                    #map set roomdesc $map[room][desc];
                    #map set roomarea $map[room][area];
                    #map set roomdata $map[room][unit];
                    
                    #nop #map get;
                    dbg room data updated : area $map[room][area];
                };
            };
            #alias {pre} {
                #if {"%%1" != ""} {
                    
                    
                    #var pre[exit] %%1;
                    #map flag nofollow;
                    #send $pre[exit];
                    
                    #delay {2} {
                        #map find {$map[room][name]}{}{$map[room][desc]};
                        #path save f pre[path];
                        #if {&pre[path] != 0} {
                            #replace pre[path] {\;} { };
                            #list pre[path] create $pre[path];
                            
                            #map get roomvnum pre[lastvnum];
                            
                            #list pre[path] size pre[pathlen];
                            #loop 1 $pre[pathlen] i {
                                #map move $pre[path][$i];
                            };
                            #unvar i;
                            #map get roomvnum pre[targetvnum];
                            #map get roomexits pre[targetexits];
                            #list pre[targetexits] create $pre[targetexits][];
                            #map goto $pre[lastvnum];
                            
                            #if {0 == @isListMember{pre[targetexits];@reverse{$pre[exit]}}} {
                                dbg 出口 $pre[exit] 存在房间记录 $pre[targetvnum];
                            };
                        } {
                            
                            dbg 出口 $pre[exit] 还没有房间记录; 
                        };
                        #send @reverse{$pre[exit]};
                        #map flag nofollow;
                        #unvar pre;
                    };
                    
                    
                } {
                    #showme <129> Usage : pre <direction><079>;
                };
            };
            #alias {ad} {
                #undelay ad_inner_watchdog;
                
                #class ad_inner open;
                
                #var adnew 1;
                #var chukou %%1;
                #var timeout 1;
                
                #delay {ad_inner_watchdog} {
                    #if {$timeout == 1} {
                        #class ad_inner kill;
                        #showme <139>->ad timeout<079>;
                    };
                } {3};
                #act {这个方向没有出路} {
                    #undelay ad_inner_watchdog;
                    #class ad_inner kill;
                    #showme <139>->ad no exit<079>;
                };
                #class ad_inner close;
                
                #send $chukou;
            };
            #class map_start_inner close;
        };
        #default {
            #showme <129>Usage : map <load | pad | save | reload | start | stop><079>;
        };
    };
};

#alias {on_map_pad} {
    #alias cb_on_map_pad #cr;
    #alias cb_on_map_pad %0;
    
    #act {^->map padding complete} {
        #unact {^->map padding complete};
        #delay {1} {cb_on_map_pad};
    };
    
    map pad;
};
#alias {on_handle_roomdesc} {
    #alias cb_on_handle_roomdesc #cr;
    #alias cb_on_handle_roomdesc %0;
    
    #act {^== 未完继续}{#cr};
    #act {_hnd_roomdesc : Get room desc complete} {
        cb_on_handle_roomdesc;
    };
};
#alias {on_get_room} {
    #class on_get_room_inner open;
    
    #alias cb_get_current_room #cr;
    #alias cb_get_current_room %0;
    
    event_handler_add {_hnd_roomdesc} {RECEIVED LINE};
    on_handle_roomdesc {
        event_handler_del {_hnd_roomdesc} {RECEIVED LINE};
        cb_get_current_room;
        #class on_get_room_inner kill;
    };
    
    #class on_get_room_inner close;
    #send look;
};
#alias {get_area} {
    #class get_area_inner1 open;
    
    
    #alias {get_area_end} {
        #class get_area_inner1 kill;
        #delay {dl_get_area} {l
            get_area_map_end;
        } {2};    
    };
    #act {这里暂时没有地图} {
        #var map[room][area] {};
        get_area_end;        
        
        dbg 当前房间没有地图信息
    };
    #act {^【%*地图】} {
        #var map[room][area] %%1;
        get_area_end;
    };
    #act {^【%*地图－%*】} {
        #var map[room][area] %%1%%2;
        #nop #var map[room][area2] %%2;
        get_area_end;
    };
    #class get_area_inner1 close;
    
    #class get_area_inner2 open;
    #alias cb_get_area #cr;
    #alias cb_get_area %0;
    #alias {get_area_map_end} {
        cb_get_area;
        #class get_area_inner2 kill;
        dbg area changed to : $map[room][area];
    };
    #act {== 未完继续} {
        #send q;
    };
    #act {[临时存储讯息]} {
        #undelay {dl_get_area};
        get_area_map_end;
    };
    #class get_area_inner2 close;
    #send localmaps;
};
#alias {show_room_data} {
    #map get roomexits tmp_room_data[knownexit];
    #list tmp_room_data[knownexit] create $tmp_room_data[knownexit][];
    
    #foreach {$map[room][exit][%*]} {eett} {
        #list tmp_room_data[knownexit] find {$eett} idx;
        #if {$idx == 0} {
            #list tmp_room_data[unknownexit] add $eett;
        }; 
    };

    #showme ->;
    #showme <139>->房间出口:$map[room][exit]<079>;
    #showme <119>->未知出口:$tmp_room_data[unknownexit]<079>;
    #showme <129>->已知出口:$tmp_room_data[knownexit]<079>;
    #map get roomarea tmp_room_data[area];
    #map get roomname tmp_room_data[name];
    #map get roomdesc tmp_room_data[desc];
    #showme <129>->已知描述:$tmp_room_data[area] => $tmp_room_data[name] => $tmp_room_data[desc]<079>;
    #nop #showme ->$map[room][area] => ${map[room][name]} => ${map[room][desc]};
    #showme ->;
    
    #unvar tmp_room_data;#unvar eett;#unvar idx;
};

#alias {carto} {
    #if {"%2" == ""} {
        #map dig to%1;
    } {
        #map link to%1 %2;
    };
    #map exit to%1 COMMAND {qu %1};
};

#nop 你一进戒律院，...只听几声大喝，如同炸雷般在大殿里回响  #map goto 1466;
#nop 执法僧兵说道：「戒律院玄痛大师请你去陈述此次下山经过 ！」;
#nop 你战战兢兢，晃晃悠悠地向对面走去......;
#nop 你战战兢兢，晃晃悠悠地向对面走去......;
#nop 你终于来到了对面，心里的石头终于落地。;

#nop 一叶扁舟缓缓地驶了过来，艄公将一块踏脚板搭上堤岸，以便乘客;
#nop 岸边一只渡船上的老艄公说道：正等着你呢，上来吧。;
#nop 唐三杰道：你老人家快请上船;
#nop 唐三杰接过你递给的船资一两白银五十文铜板;

#nop 芣苢向上官八富打听有关『过河』的消息。;
#nop 上官八富鼓足中气，长啸一声：“船家！”;
#nop 一叶扁舟缓缓地驶了过来，艄公将一块踏脚板搭上堤岸，以便乘客;
#nop 上下。;
#nop 芣苢向上官八富打听有关『过江』的消息。;
#nop 上官八富不耐烦的瞪了芣苢一眼，说道：没看我正忙着吗？;
#nop 芣苢吸了口气，一声“船家”，声音中正平和地远远传了出去。;
#nop 岸边一只渡船上的老艄公说道：正等着你呢，上来吧。;
#nop 芣苢往里离开。;

#nop 你只觉得离塘沽口越来越远，不一会儿就再也看不见了。;
#nop 你感觉过了好长时间，正在昏昏欲睡间，感觉船震了一下，原来是靠岸了。;

#nop 你级别不够，不能进入达摩院
#nop 你的动作还没有完成，不能移动;
#nop 你不小心被什么东西绊了一下，差点摔个大跟头;

#alias {on_there} {
    #alias cb_on_there #cr;
    #alias cb_on_there %0;
    
    #act {到达目的地} {
        #unact {到达目的地};
        cb_on_there;
        #unalias cb_on_there;
    };
};
#alias {on_error} {
    #alias cb_on_error #cr;
    #alias cb_on_error %0;
    
    #act {行走出错} {
        #unact {行走出错};
        cb_on_error;
        #unalias cb_on_error;
    };
};
#alias {mess} {
    #message  ACTIONS               %1;
    #message  ALIASES               %1;
    #message  CLASSES               %1;
    #message  COMMANDS              %1;
    #message  CONFIGURATIONS        %1;
    #message  DELAYS                %1;
    #message  EVENTS                %1;
    #message  FUNCTIONS             %1;
    #message  GAGS                  %1;
    #message  HIGHLIGHTS            %1;
    #message  HISTORIES             %1;
    #message  MACROS                %1;
    #message  PATHS                 %1;
    #message  PATHDIRS              %1;
    #message  PROMPTS               %1;
    #message  SUBSTITUTIONS         %1;
    #message  TABS                  %1;
    #message  TICKERS               %1;
    #message  VARIABLES             %1;
};

#alias {find_path} {
    
    #if {"%1" != ""} {
        #var map[gt][dest][name] {%1};
        #var map[gt][dest][area] {%2};
    };
    
    #class find_path_inner open;
    
    #alias {path_save} {
        #path save forward map[gt][path];
        #if {"$map[gt][path]" != ""} {
            #var map[gt][path_found] 1;
        };
    };
    
    #class find_path_inner close;
    
    mess off;
    
    #var map[gt][path_found] 0;
    #var map[gt][path] {};
    
    #map get roomname map[room][name];
    #map get roomarea map[room][area];
    #map get roomvnum map[room][vnum];
    
    #if {"$map[gt][dest][area]" == ""} {
        #map get roomvnum map[room][vnum];
        
        #if {"$map[room][name]" == "$map[gt][dest][name]" ||
            "$map[room][vnum]" == "$map[gt][dest][name]"} {
            #showme <139> GPS : 目的地为当前房间。<079>;
            
            #class find_path_inner kill;
            #var map[gt][path_found] 2;
            #return;
        };
    } {
        #if {"$map[room][name]" == "$map[gt][dest][name]" &&
            "$map[room][area]" == "%*$map[gt][dest][area]%*"} {
            
            #showme <139> GPS : 目的地为当前房间。<079>;
            
            #class find_path_inner kill;
            #var map[gt][path_found] 2;
            #return;
        };
    };

    #if {"$map[gt][dest][area]" == ""} {
        #if {0 == $map[gt][path_found]} {
            #map find {$map[gt][dest][name]};
            path_save;
        };
        #if {0 == $map[gt][path_found]} {
            #map find {%*$map[gt][dest][name]%*};
            path_save;
        };
        #if {0 == $map[gt][path_found]} {
            #map find {}{}{}{}{$map[gt][dest][name]};
            path_save;
        };
        #if {0 == $map[gt][path_found]} {
            #map find {}{}{}{}{%*$map[gt][dest][name]%*};
            path_save;
        };
    } {
        #if {0 == $map[gt][path_found]} {
            #map find {$map[gt][dest][name]} {} {} {$map[gt][dest][area]};
            path_save;
        };
        #if {0 == $map[gt][path_found]} {
            #map find {%*$map[gt][dest][name]%*} {} {} {$map[gt][dest][area]};
            path_save;
        };
        #if {0 == $map[gt][path_found]} {
            #map find {%*$map[gt][dest][name]%*}{}{}{%*$map[gt][dest][area]%*};
            path_save;
        };
        #if {0 == $map[gt][path_found]} {
            #map find {} {} {} {$map[gt][dest][area]} {$map[gt][dest][name]};
            path_save;
        };
        #if {0 == $map[gt][path_found]} {
            #map find {} {} {} {$map[gt][dest][area]} {%*$map[gt][dest][name]%*};
            path_save;
        };
        #if {0 == $map[gt][path_found]} {
            #map find {} {} {} {%*$map[gt][dest][area]%*} {%*$map[gt][dest][name]%*};
            path_save;
        };
    };

    #class find_path_inner kill;
    mess on;
};
#alias {hire_boat} {
    ask shao gong about 过河;
    ask shao gong about 过江;
    enter;
};
#alias {gts} {
    goto_stop;
};
#alias {goto_stop} {
    set brief 0;
    #show <139>GPS: 行走停止<079>;
    #class go_forward_inner kill;
    #class _path_run_inner kill;
    #class goto_inner kill;
    #var map[gt][status] 0;
    #undelay {dl_wait_need};
    #delay {dl_hubiao_walk};
    #delay {dl_trvs_walk};
};

#alias {gt} {
    #if {"%1" == ""} {
        #showme <129> Usage : gt <roomname> [area]<079>;
        #return;
    };
    goto %1 %2;
};
#alias {goto} {
    #nop param1 roomname;
    #nop param2 roomarea;
    
    #if {$map[gt][status] == 1} {
        #return;
    };
    #elseif {$map[gt][status] == 0} {
        #var map[gt][status] 1;
    };
    
    #var map[gt][dest][name] {%1};
    #var map[gt][dest][area] {%2};
    
    #class goto_inner open;
    
    #alias {gt_err} {
        #showme <119> GPS : %%0<079>;
    };
    #alias {gt_msg} {
        #showme <129> GPS : %%0<079>;
    };
    #alias {gt_wrn} {
        #showme <139> GPS : %%0<079>;
    };
    #class goto_inner close;
    
    #if {"%1" == ""} {
        gt_msg goto <roomname> [roomarea];
        #var map[gt][status] 0;
    } {
        find_path;
        
        #if {$map[gt][path_found] == 2} {
            #var map[gt][status] 0;
        } {
            #if {0 == $map[gt][path_found]} {
                gt_wrn 目标地点不可达;
                
                #class goto_inner kill;
                #var map[gt][status] 0;        
            } {
                gt_msg 找到路径 {$map[gt][path]};
                
                on_error {
                    #var map[gt][status] 0;
                    #class goto_inner kill;
                };
                
                _path_run;
            };
        };
    };
};

#alias {_path_run} {
    status.set_action 走路中;
    
    #var map[gt][mode] {%1};
    
    #class _path_run_inner open;
    
    #var path_run {};
    
    #if {"%1" == "trvs" }{
        #var path_run[list] {$map[trvs][path]};
    } {
        #list path_run[list] add $map[gt][path];
    };
    
    #list path_run[list] size path_run[list_len];
    #var path_run[list_idx] 0;
    
    #alias {back_one_step} {
        #math path_run[list_idx] {$path_run[list_idx] - 1};
    };
    #alias {do_rewalk} {
        #map undo;
        back_one_step;
        #delay {1} {
            go_forward;
        };
    };
    #alias {go_forward} {
        
        #math path_run[list_idx] {$path_run[list_idx] + 1};
        #if {$path_run[list_idx] > $path_run[list_len]} {
            #act {^设定环境变量：brief = 0} {
                #undelay {dl_check_walkend};
                #unact {^设定环境变量：brief = 0};
                #showme <129> GPS : 到达目的地<079>;
            };
            
            #class _path_run_inner kill;
            #class on_blocked_inner kill;
            #class on_rewalk_need_inner kill;
            #class on_wait_need_inner kill;
            
            #var map[gt][status] 0;
            
            #class goto_inner kill;
            
            status.set_action 空闲;
            #delay {dl_check_walkend} {
                #unact {^设定环境变量：brief = 0};
                #showme <129> GPS : 到达目的地<079>;
            } {5};
            set brief 0;
        } {
            #class go_forward_inner open;
            
            #act {^走到这里，你被壮阔的景色所吸引，向北眺望，正是长江最险峻湍急之处} {
                on_walked {
                    on_walked {
                        #class go_forward_inner kill;
                        go_forward;
                    };
                };
                
            };
            on_blocked {
                #map undo;
                
                #class go_forward_inner kill;
                #class _path_run_inner kill;
                #class on_rewalk_need_inner kill;
                
                set brief 0;
                
                gt_wrn 行走出错;
                
                status.set_action 空闲;
            };
            on_rewalk_need {
                #nop #show 需要重走 $path_run[list][$path_run[list_idx]];
                #map undo;
                back_one_step;
                #delay {2} {
                    go_forward;
                };
            };
            on_wait_need {
                log 需要等待60秒再继续走路;
                #map undo;
                back_one_step;
                #delay {dl_wait_need} {
                    go_forward;
                }{60};
            };
            
            #class go_forward_inner close;
            
            #if {"$path_run[list][$path_run[list_idx]]" == "to%*"} {
                on_car_arrived {
                    on_walked {
                        #class go_forward_inner kill;
                        go_forward;
                    };
                    xia;
                };
            };
            #elseif {"$path_run[list][$path_run[list_idx]]" == "%*boat"} {
                on_boat_arrived {
                    on_walked {
                        #undelay {wd_boat_arrived};
                        #class go_forward_inner kill;
                        go_forward;
                    };
                    #delay {wd_boat_arrived}{look}{5};
                    out;
                    #nop go_forward;
                };
            };
            #elseif {"$path_run[list][$path_run[list_idx]]" == "climb%*" ||
                     "$path_run[list][$path_run[list_idx]]" == "guo%*" ||
                     "$path_run[list][$path_run[list_idx]]" == "zou%*" } {
                on_action_done {
                    on_walked {
                        #class go_forward_inner kill;
                        go_forward;
                    };
                    #nop go_forward;
                };
            };
            #else {
                on_walked {
                    #class go_forward_inner kill;
                    go_forward;
                };
            };
            
            #nop #show $path_run[list][$path_run[list_idx]];
            
            #if {"$map[gt][mode]" == "trvs"} {
                #delay {dl_trvs_walk} {
                    ${path_run[list][$path_run[list_idx]]};
                }{1};
            };
            #else {
                $path_run[list][$path_run[list_idx]];
            };
        };
        
    };
    #alias {on_walked} {
        #alias cb_on_walked #cr;
        #alias cb_on_walked %%0;
        
        #nop #act {^%S%s-%s$};
        #nop #act {^%* - $};
        #nop #act {^%* - [{城内|野外|门派}]} {
        #nop    #unact {^%* - [{城内|野外|门派}]};
        #nop     cb_on_walked;
        #nop };
        #act {^%* -  $} {
            #unact {^%* -  $};
            cb_on_walked;
        };
    };
    #alias {on_wait_need} {
        #alias cb_on_wait_need #cr;
        #alias cb_on_wait_need %%0;
        
        #class on_wait_need_inner open;
        
        #list on_wait_need_trigger_list create {
            ^你刚要前行，忽然发现江水决堤，不由暗自庆幸，还好没过去;
            ^你正要前行，有人大喝：黄河决堤啦，快跑啊;
        };
        
        #foreach {$on_wait_need_trigger_list[%*]} {regstring} {
            #act {$regstring} {
                #class on_wait_need_inner kill;
                cb_on_wait_need;
            };
        };
        
        #class on_wait_need_inner close;
    };
    #alias {on_blocked} {
        #alias cb_on_blocked #cr;
        #alias cb_on_blocked %%0;
        
        #class on_blocked_inner open;
        
        #list on_blocked_trigger_list create {
            ^这个方向没有出路;
            ^哎哟，你一头撞在墙上;
            ^你从悬崖上摔了下来;
            ^那条路不知深浅，还是不要去了;
            ^你身上没有车资%*车夫拒绝开车;
            ^陆大有喝道：你不是华山弟子，不得入内;
            ^童百熊说道：「你不是我日月神教弟子，来我教干什么？速速离开！」;
            ^江百胜伸手拦住你说道：盟主很忙，现在不见外客，你下山去吧;
            ^清兵说道：赶快滚开，这是你停留的地方吗;
            ^守山弟子说: 非我派弟子不许上峨嵋山;
            ^守山弟子说: 峨嵋非行凶之地，施主想要上山请解兵器;
            ^守卫喝道：闲杂人等，不得乱闯;
            ^褚万里喝道：闲杂人等，不得入内;
            ^王府亲兵大声喝道：「哪里来的刁民，胆敢擅闯王府？」;
            ^校尉挡住了你的去路：大将军不在府上;
            ^梅剑伸手拦住你，说道：“非灵鹫宫弟子请回！”;
            ^壮年僧人说道：这位施主请回罢，本寺不接待俗人;
            ^石阶越来越险，你提心吊胆的走着，终于一个不小心滚了下去;
            ^厚土旗众大声吼道：“你不是明教弟子，不许上山！”;
            ^明教休息室，外派弟子不得入内;
            ^官兵拦住你说道;
            ^就凭你这点武功，最好不要瞅热闹了，小心被潮水卷跑了;
            ^你身上背的东西太重，洞口又太高，你够不着;
            ^红花会众说道：“阁下不是红花会弟子，最好不要进去;
            ^你觉得贸然进去有些不妥，还是先问问傻姑和程英再说吧;
            ^我劝你不要轻举妄动;
            ^门卫把手一拦：你这种正派人物，老子一看就恶心，快滚;
            ^神龙教弟子大声喝道：本教重地，外人不得入内;
            ^神龙教弟子大喝一声：里面是教主和夫人休息之低，外人不得入内;
            ^桑结伸手拦住了你;
            ^护法喇嘛拦住你道：那里是本寺女弟子休息的居所，你去不大方便吧;
            ^杂役喇嘛拦住了你，小心翼翼地道：“施主留步，本寺方丈正在清修，不许打搅;
            ^护法喇嘛拦住你道：“后山是本寺重地，你没有方丈的手谕，不能进去;
        };
        
        #foreach {$on_blocked_trigger_list[%*]} {regstring} {
            #act {$regstring} {
                #class on_blocked_inner kill;
                cb_on_blocked;
            };
        };
        
        #class on_blocked_inner close;
    };
    #alias {on_rewalk_need} {
        #alias cb_on_rewalk_need #cr;
        #alias cb_on_rewalk_need %%0;
        
        #class on_rewalk_need_inner open;
        
        #list on_rewalk_need_trigger_list create {
            ^吊桥还没有升起来，你就这样走了，可能会给外敌可乘之机的;
            ^你的动作还没有完成，不能移动;
            ^僧兵上前把大门关了起来;
            ^乒地一声，里面有人把大门关上了;
            ^只听得江面上隐隐传来：“别急嘛，这儿正忙着呐;
            ^你逃跑失败;
            ^沙石地几乎没有路了，你走不了那么快;
            ^你小心翼翼往前挪动，遇到艰险难行处，只好放慢脚步;
            ^你还在山中跋涉，一时半会恐怕走不出这六盘山;
            ^你还在山中跋涉，一时半会恐怕走不出这西南地绵绵群山;
            ^你还在山中跋涉，一时半会恐怕走不出这藏边群山;
            ^青海湖畔美不胜收，你不由停下脚步，欣赏起了风景;
            ^你不小心被什么东西绊了一下，差点摔个大跟头;
            ^荒路几乎没有路了，你走不了那么快;
        };
        
        #foreach {$on_rewalk_need_trigger_list[%*]} {regstring} {
            #act {$regstring} {
                #nop #classon_rewalk_need_inner kill;
                cb_on_rewalk_need;
            };
        };
        
        #class on_rewalk_need_inner close;
        
    };
    
    #alias {on_action_done} {
        #alias cb_on_action_done #cr;
        #alias cb_on_action_done %%0;
        
        #class on_action_done_inner open;
        
        #list on_action_done_trigger_list create {
            ^突然间蓬一声，屁股撞上了什么物事，身子向上弹起，原来恰好撞到崖边伸出的一株;
            ^一条大瀑布如玉龙悬空，滚滚而下，倾入一座清澈异常的大湖之中;
            ^你终于一步步的终于挨到了桥头;
            ^你终于来到了对面，心里的石头终于落地;
            ^你觉得好象停了下来，睁开眼一看;
        };
        
        #foreach {$on_action_done_trigger_list[%*]} {regstring} {
            #act {$regstring} {
                #class on_action_done_inner kill;
                cb_on_action_done;
            };
        };
        
        #class on_action_done_inner close;
    };
    #alias {on_car_arrived} {
        #alias cb_on_car_arrived #cr;
        #alias cb_on_car_arrived %%0;
        
        #act {^大车停稳了下来，你可以下车(xia)了} {
            #unact {^大车停稳了下来，你可以下车(xia)了};
            cb_on_car_arrived;
        };
    };
    #alias {on_boat_arrived} {
        #alias cb_on_boat_arrived #cr;
        #alias cb_on_boat_arrived %%0;
        
        #class on_boat_arrived_inner open;
        
        #nop 船夫对你说道：“到了，上岸吧。”，随即把一块踏脚板搭上堤岸;
        #nop 你感觉过了好长时间，正在昏昏欲睡间，感觉船震了一下，原来是靠岸了;
        #list boat_arrived_trigger_list create {
            ^艄公说“到啦，上岸吧”，随即把一块踏脚板搭上堤岸;
            ^你沿着踏板走了上去;
            ^你朝船夫挥了挥手便跨上岸去;
            ^不知过了多久，船终于靠岸了，你累得满头大汗;
            ^小舟终于划到近岸，你从船上走了出来;
            ^绿衣少女将小船系在树枝之上，你跨上岸去;
        };
        
        #foreach {$boat_arrived_trigger_list[%*]} {regstring} {
            #act {$regstring} {
                #class on_boat_arrived_inner kill;
                cb_on_boat_arrived;
            };
        };
        
        #class on_boat_arrived_inner close;
    };
    #class _path_run_inner close;
    
    set brief 3;
    go_forward;
};

#list area_list create {建康府诸军都统制府;西湖;杭州提督府;少林寺;北京;长江北岸;长江南岸;长江;建康府南城;建康府北城;黄河南岸;黄河北岸;丝绸之路;回族小镇;日月神教;康亲王府;平西王府;扬州;镇江;临安府;牙山;苏州;峨嵋;归云庄;大理城中;桃源;杀手帮;桃花岛;铁掌峰;岳王墓;洛阳;长安;大轮寺;凌霄城;侠客岛;冰火岛;小山村;洛阳;麒麟村;晋阳;兰州;湟中;星宿;灵鹫;全真;武当山;紫禁城;神龙岛;天龙寺;张家口;无量山;嘉兴;明州;江州;姑苏慕容;泉州;岳阳;梅庄;福州;灵州;南昌;信阳;荆州;襄阳;华山;丐帮;中原;古墓;曲阜;泰山;天坛;回部;明教;白驼山;成都;昆明;苗疆}

#var map_area_table {{杭州提督府}{临安提督府}{建康府南城}{建康府南}{建康府北城}{建康府北}{长江}{长江南岸}{大理城中}{大理城}{姑苏慕容}{慕容}{小山村}{华山村}{白驼山}{白驼}{福州}{闽南}};

#nop 西湖西湖岸边;

#alias {parse_addr} {
    #var work_place %1;
    #var buffer %2;
    
    #foreach {$area_list[%*]} {areaname} {
        #regexp {$work_place}{^$areaname%*} {
            #var roomname {&1};
            
            #if {"$roomname" == "红娘%*"} {
                #var areaname {noarea};
                #var roomname {红娘庄};
            };
            #if {"$roomname" == "荆州" || "$roomname" == "荆门" || "$roomname" == "华容道"} {
                #var areaname {荆州};
            };
            #if {"$work_place" == "襄阳官道"} {
                #var areaname {荆州};
                #var roomname {官道};
            };
            #if {"$work_place" == "泉州杂货铺"} {
                #var areaname {泉州};
                #var roomname {曾宅};
            };
            #if {"$work_place" == "中原客店二楼"} {
                #var areaname {中原};
                #var roomname {悦来客栈};
            };
            #if {&map_area_table[$areaname] == 0} {
                #var $buffer {{area}{$areaname}{name}{$roomname}};
            } {
                #var $buffer {{area}{$map_area_table[$areaname]}{name}{$roomname}};
            };
            
            #unvar areaname;
            #unvar work_place;
            #unvar roomname;
            #break;
        };
    };
};

#function {parse_addr} {
    #var work_place %1;
    
    #foreach {$area_list[%*]} {areaname} {
        #regexp {$work_place}{^$areaname%*} {
        
            #if {"&1" == "红娘%*"} {
                #var result {{area}{}{name}{&1}};
            };
            #if {"&1" == "荆门"} {
                #var result {{area}{}{name}{&1}};
            };
            #else {
                #if {&map_area_table[$areaname] == 0} {
                    #var result {{area}{$areaname}{name}{&1}};
                } {
                    #var result {{area}{$map_area_table[$areaname]}{name}{&1}};
                };
            };
            
            #unvar areaname;#unvar work_place;
            #break;
        };
    };
};

#alias {gotowork} {
    #var work_place %1;
    
    #foreach {$area_list[%*]} {areaname} {
        #regexp {$work_place}{^$areaname%*} {
        
            #if {"&1" == "红娘%*"} {
                goto &1;
            };
            #if {"&1" == "荆门"} {
                goto &1;
            };
            #else {
                #if {&map_area_table[$areaname] == 0} {
                    goto &1 $areaname;
                } {
                    goto &1 $map_area_table[$areaname];
                };
            };
            
            log $areaname &1;
            
            #unvar areaname;#unvar work_place;
            #break;
        };
    };
};

#nop 2923 明教 2245 白驼 1147 兰州 1009 大轮寺 65 苏州;
#list bus_station_list create {63;65;93;95;170;187;198;242;244;284;286;322;337;797;988;990;992;1011;1053;1076;1107;1109;1139;1171;1182;1210;1347;1533;1582;1584;1677;1691;1741;2101;3288;3289;3290}

#alias {map.car} {
    #map get roomvnum current_room_id;
    
    #if {"%1" == "on"} {
        #foreach {$bus_station_list[%*]} {room_id} {
            #map goto $room_id;
            #map roomflag avoid off;
        };
        #showme <139>GPS : 开启马车节点<079>;
    };
    #elseif {"%1" == "off"} {
        #foreach {$bus_station_list[%*]} {room_id} {
            #map goto $room_id;
            #map roomflag avoid on;
        };
        #showme <139>GPS : 关闭马车节点<079>;
    };
    
    #map goto $current_room_id;
    #unvar current_room_id;
};

#nop 1111 树洞内部 1112 树洞下
#list andao_list create {1112};

#alias {map.andao} {
    #map get roomvnum current_room_id;
    
    #if {"%1" == "on"} {
        #foreach {$andao_list[%*]} {room_id} {
            #map goto $room_id;
            #map roomflag avoid off;
        };
        #showme <139>GPS : 开启暗道节点<079>;
    };
    #elseif {"%1" == "off"} {
        #foreach {$andao_list[%*]} {room_id} {
            #map goto $room_id;
            #map roomflag avoid on;
        };
        #showme <139>GPS : 关闭暗道节点<079>;
    };
    
    #map goto $current_room_id;
    #unvar current_room_id;
};

#nop =========================== 遍历 =========================================;

#alias {on_get_exits} {
    #alias cb_on_get_exits #cr;
    #alias cb_on_get_exits %0;
    
    #act {^%s这里%*{方向|出口}{是|有}%*} {
        #unact {^%s这里%*{方向|出口}{是|有}%*};
        #nop #var map[trvs][leaf] @format_exits{%%5};
        #var trvs[exits] %%5;
        #replace {trvs[exits]} {。} {};
	    #replace {trvs[exits]} { } {};
	    #replace {trvs[exits]} {和} { };
	    #replace {trvs[exits]} {、} { };
	    
	    #list 
        cb_on_get_exits;
    };
    look;
};

#alias {_visit} {
    #var return_path @reverse{%1};
    %1;
    #delay {1} {$return_path};
};
#alias {_trvs} {
    #if {"$map[trvs][parent]" == "rootnode"} {
        
    } {
    
    };
};
#alias {traverse} {
    #class traverse_inner open;
    
    #var map[trvs][need_deep] 1;
    #var map[trvs][need_deep] %1;
    #var map[trvs][curr_deep] 0;
    
    #var map[trvs][rootnode] {};
    #var map[trvs][leaf] {};
    #var map[trvs][parent] {rootnode};
    
    on_get_exits {
        
    };
    #class traverse_inner close;
};

#alias {bfs} {
    #var map[trvs][path] {};
    
    #var bfs[target_level] 1;
    #var bfs[target_level] %1;
    #var bfs[level] -1;
    
    #var bfs[q1] {};
    #var bfs[q2] {};
        
    #var bfs[nodes_v] {};
    #var bfs[nodes_e] {};
    
    #var bfs[disabled_vnum] {{vnum}{name}};
    
    #map get roomvnum bfs[start_pos];
    #map get roomarea bfs[area];
    
    #var bfs[q2][$bfs[start_pos]] {};
    
    #while {$bfs[level] < $bfs[target_level]} {
    
        #var bfs[q1] $bfs[q2];
        #var bfs[q2] {};
        
        #foreach {$bfs[q1][]} {bfs[q1_k]} {
            
            #if {&bfs[disabled_vnum][$bfs[q1_k]] == 0} {
                #map goto $bfs[q1_k];
                
                #map get roomarea bfs[next_node][area];
                #map get roomname bfs[next_node][name];
                
                #if {"$bfs[next_node][name]" != "马车" &&
                    "$bfs[next_node][name]" != "小船"} {
                    
                    #if {"$bfs[area]" == "" ||
                         "$bfs[next_node][area]" == "" ||
                         "$bfs[area]" == "$bfs[next_node][area]"} {
                        
                        #map get roomexits bfs[next_node][exits];
                        
                        #var bfs[nodes_v][$bfs[q1_k]] $bfs[next_node][name];
                        #var bfs[nodes_e][$bfs[q1_k]] {$bfs[next_node][exits]};
                        
                        #foreach {$bfs[next_node][exits][%*]}{bfs[v]} {
                            #var bfs[q2][$bfs[v]] {};
                        };
                    };
                };
            };
        };
        
        #math bfs[level] {$bfs[level] + 1};
    };
    
    #unvar bfs[q1];
    #unvar bfs[q2];
    #unvar bfs[next_node];
    #unvar bfs[q1_k];
    #unvar bfs[v];
    
    #map goto $bfs[start_pos];
    
    mst $bfs[start_pos] bfs[nodes_v] bfs[nodes_e];
    #unvar bfs;
};

#alias {mst} {
    #var mst[start] %1;
    #var mst[v] ${%2};
    #var mst[e] ${%3};
    
    #var mst[path] {};
    #var mst[visited] {};
    
    #var mst[buffer] {};
    
    #var mst[buffer][vnum] $mst[start];
    
    #var mst[visited][$mst[buffer][vnum]] $mst[v][$mst[buffer][vnum]];
    #unvar mst[v][$mst[buffer][vnum]];
    
    #var mst[buffer][last_vnum] 0;
    #nop #list mst[buffer][stack] add $mst[buffer][vnum];
    
    #while {&mst[v][] != 0} {
        #var mst[buffer][path] {};
        #var mst[buffer][r_path] {};
        #var mst[buffer][exits] $mst[e][$mst[buffer][vnum]];
        
        #foreach {$mst[buffer][exits][]} {mst[buffer][exit]} {
        
            #if {&mst[e][$mst[buffer][exits][$mst[buffer][exit]]] != 0} {
            
                #if {&mst[visited][$mst[buffer][exits][$mst[buffer][exit]]] == 0} {
                    #var mst[buffer][path] $mst[buffer][exit];
                    #break;
                };
            };
        };
        
        #if {"$mst[buffer][path]" == ""} {
            #if {$mst[buffer][vnum] != $mst[start]} {
            
                #var mst[buffer][last_vnum] $mst[buffer][stack][-1];
                #list mst[buffer][stack] delete -1;
                
                #foreach {$mst[buffer][exits][]}{mst[buffer][exit]}{
                    #if {$mst[buffer][exits][$mst[buffer][exit]] == $mst[buffer][last_vnum]} {
                        #var mst[buffer][path] $mst[buffer][exit];
                        #break;
                    };
                };
                
                #if {"$mst[buffer][path]" == ""} {
                    #show 遍历完成,没有返回路径;
                    #break;
                };
            } {
                #show 遍历完成,回到起点;
                #break;
            };
        } {
            #nop #show delete $mst[v][$mst[buffer][exits][$mst[buffer][path]]] $mst[v][$mst[v][$mst[buffer][exits][$mst[buffer][path]]]];
            
            #list mst[buffer][stack] add $mst[buffer][vnum]; 
            #unvar mst[v][$mst[buffer][exits][$mst[buffer][path]]];
        };
        
        #list mst[path] add $mst[buffer][path];
        
        #var mst[buffer][vnum] $mst[buffer][exits][$mst[buffer][path]];
        #var mst[visited][$mst[buffer][vnum]] $mst[v][$mst[buffer][vnum]];
        
    };
    
    #show 节点遍历完成;
    
    #var map[trvs][path] $mst[path];
    
    #show 遍历路径:$map[trvs][path];
    
    #unvar mst;
    
    #nop trvs.start;
};

#alias {trvs.reset} {
    #var map[trvs][idx] 1;
};
#alias {trvs.start} {
    #class trvs.inner open;
    
    #if {"%1" != ""} {
        #var on_trvs_complete_handler %1;
        $on_trvs_complete_handler {
            trvs.stop;
        };
    };
    
    #act {^%s这里%*{方向|出口}{是|有}%*} {
        #delay {dl_trvs_step} { trvs.step } {1};
    };
    
    #class trvs.inner close;
    
    trvs.reset;
    trvs.step;
};
#alias {trvs.step} {
    
    #if {"$map[trvs][path]" != ""} {
        #if {&map[trvs][path][$map[trvs][idx]] != 0} {
            #act {^你逃跑失败} {
                #unact {^你逃跑失败};
                #math map[trvs][idx] {$map[trvs][idx] - 1};
            };
            $map[trvs][path][$map[trvs][idx]];
            #math map[trvs][idx] {$map[trvs][idx] + 1};
        } {
            trvs.stop;
            #unact {^你逃跑失败};
            #show <139>遍历路径行走完成<099>;
        };
    } {
        trvs.stop;
        #show 遍历路径为空;
    };
};
#alias {trvs.stop} {
    #undelay {dl_trvs_step};
    #class trvs.inner kill;
};

#nop ================================== 常用行走别名 ===========================;

#alias {rbz} {
    goto 荣宝斋;
};
#alias {dp} {
    goto %*当铺;
};
#alias {qz} {
    goto %*钱庄;
};
#alias {wm} {
    goto 武庙;
};
#alias {bw} {
    gt 帅府大厅;
};
#alias {pad} {
    map pad;
};
#alias {gohome} {
    goto %*盛昌%*;
    on_there {
        enter @lower{$status[id]};
    };
};

#class $mod_name close;

${mod_name}_load;
